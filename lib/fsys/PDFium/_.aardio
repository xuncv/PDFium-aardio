namespace fsys
import console
import util.metaProperty;
import gdip
class PDFium{
	ctor( pdfPath,password="" ){
		this.LIBRARY_CONFIG = {
			int version = 2;
			pointer m_pUserFontPaths = 0;
			pointer m_pIsolate = 0;
			INT m_v8EmbedderSlot = 0;
			pointer m_pPlatform = 0;
		}
		dll.FPDF_InitLibraryWithConfig(topointer(this.LIBRARY_CONFIG))
		this.doc = dll.FPDF_LoadDocumentP(pdfPath,password)
		..table.gc(this,"destroy")	
	};
	
	loadPage = function(idx=0){
		if(this.currentPage){
			dll.FPDF_ClosePage(this.currentPage);
			this.currentPage = null
		}
		this.currentPage = dll.FPDF_LoadPageP(this.doc,idx)
	}
	
	extractBookmarks = function(){
		var h = 0
		var bookmarks = self.bookmark(this.doc,h);
		return bookmarks
	}
	
	//结构化输出 todo
	
	getMeta = function(){
		var keys = {"Title";"Author";"Subject";"Keywords";"Creator";"Producer";"CreationDate";"ModDate"}
		var meta = {}
		for(k,v in keys){
			var len = dll.FPDF_GetMetaText(this.doc,v,null,null)
			var buffer = ..raw.buffer(len)
			len = dll.FPDF_GetMetaText(this.doc,v,buffer,len)
			meta[v] = ..string.fromUnicode( buffer )
			buffer = null
		}
		return meta
	}
	
	extractText = function(pageNum=0){
		if(pageNum<0 || pageNum>owner.pageCount){
			assert(false,"页码超过pdf范围")
		}
		this.loadPage(pageNum);
		var textPage = dll.FPDFText_LoadPageP(this.currentPage);
		var textCountChars = dll.FPDFText_CountChars(textPage);
		var buffer = ..raw.buffer( textCountChars*2 )
		var num = dll.FPDFText_GetText(textPage,0,textCountChars,buffer)
		return ..string.fromUnicode( buffer )
	}
	
	// #define FPDFBitmap_BGR 2
	asBitmap = function(pageNum){
		if(pageNum<0 || pageNum>owner.pageCount){
			assert(false,"页码超过pdf范围")
		}
		this.loadPage(pageNum);
		//var bit = dll.FPDFBitmap_CreateExP( owner.pageWidth,owner.pageHeight,2,null,null )
		var bit = dll.FPDFBitmap_CreateP( owner.pageWidth,owner.pageHeight,0 )
		dll.FPDFBitmap_FillRect(bit,0,0,owner.pageWidth,owner.pageHeight,0xFFFFFFFF);
		dll.FPDF_RenderPageBitmap(bit,this.currentPage,0,0,owner.pageWidth,owner.pageHeight,0,0x02 | 0x01);
		var buffer = dll.FPDFBitmap_GetBufferP(bit);
		var stride = dll.FPDFBitmap_GetStride(bit)
		var bitmap,err = ..gdip.bitmap( owner.pageWidth,owner.pageHeight,0x26200A/*_PixelFormat32bppARGB*/,buffer,stride )
		if(!bitmap) dll.FPDFBitmap_Destroy(bit)
		return bitmap,err; 
	}
	
	destroy = function(){
		dll.FPDF_CloseDocument(this.doc)
		dll.FPDF_DestroyLibrary()
	}
	@_metaProperty;	
}

namespace PDFium{
	dll = ..raw.loadDll( $"~\lib\fsys\PDFium\.dll\pdfium.dll",,"cdecl" )
	class bookmark{
		ctor( doc,handle ){
			this.doc = doc;
			this.handle = handle;
		};

		asTree = function(){
			var treeData = {}; 
			var enumInfo;
			enumInfo = function(info,td,parent){  
				for(k,v in info.children){    
					if( v.children ){
						var children = {text = v.title;pageIndex = v.pageIndex}
						..table.push(td,children); 
						enumInfo(v,children,info); 
					}
					else {
						..table.push(td,{text = v.title;pageIndex = v.pageIndex;}); 
					} 
				}
			} 
			enumInfo(this,treeData);
			return treeData; 
		};
		
		@_bmMeta;
	}
	_metaProperty = ..util.metaProperty(
		pageCount = {
			_get = function(){ 
				return dll.FPDF_GetPageCount(owner.doc)
			}	 
		};
		pageWidth = {
			_get = function(){
				if(!owner.currentPage) owner.loadPage()
				return ..math.round( dll.FPDF_GetPageWidthF(owner.currentPage) ); 
			}
		}

		pageHeight = {
			_get = function(){
				if(!owner.currentPage) owner.loadPage()
				return ..math.round( dll.FPDF_GetPageHeightF(owner.currentPage) ); 
			}
		}
	)
}

namespace PDFium.bookmark{
    var dll = ..fsys.PDFium.dll
	_bmMeta = ..util.metaProperty(
    	children = {
    		_get = function(){
    			var c = {}
    			var h = dll.FPDFBookmark_GetFirstChildP(owner.doc,owner.handle)
    			if(h){
					do{
    					..table.push( c,..fsys.PDFium.bookmark(owner.doc,h) )
    					h = dll.FPDFBookmark_GetNextSiblingP(owner.doc,h)
    				}while(h)
    			}
    			return c; 
    		} 	
    	}
    	
    	title = {
    		_get = function(){ 
				var len = dll.FPDFBookmark_GetTitle(owner.handle,0,0)
				var buffer = ..raw.buffer( len*2 )
				len = dll.FPDFBookmark_GetTitle(owner.handle,buffer,len)
				return ..string.fromUnicode(buffer);
    		}	
    	};
    	
    	pageIndex = {
    		_get = function(){
				var dst = dll.FPDFBookmark_GetDestP(owner.doc,owner.handle)
				var pageIndex = dll.FPDFDest_GetDestPageIndex(owner.doc,dst)
				return pageIndex; 	
    		}	   	
    	}
	)	
}


/**intellisense()
fsys.PDFium = 基于Google开源的PDFium库,封装的pdf解析库
fsys.PDFium() = !PDFium.
fsys.PDFium(.(pdf路径或pdf数据,pdf密码) = 创建pdf解析对象
end intellisense**/

/**intellisense(!PDFium)
loadPage(.(页码) = 载入某一页
extractBookmarks() = 提取目录,返回bookmark对象
getMeta() = 获取pdf属性表
extractText(.(页码) = 提取当前页文本
asBitmap(.(页码) = 当前页转换为bitmap对象
end intellisense**/

 /**intellisense()
_FPDFBitmap_Unknown=@0/*_FPDFBitmap_Unknown*/
_FPDFBitmap_Gray=@1/*_FPDFBitmap_Gray*/
_FPDFBitmap_BGR=@2/*_FPDFBitmap_BGR*/
_FPDFBitmap_BGRx=@3/*_FPDFBitmap_BGRx*/
_FPDFBitmap_BGRA=@4/*_FPDFBitmap_BGRA*/
end intellisense**/