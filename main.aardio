import win.ui;
/*DSG{{*/
mainForm = win.form(text="pdfDemo";right=1050;bottom=620)
mainForm.add(
button={cls="button";text="提取文本";left=916;top=349;right=1014;bottom=381;z=9};
button2={cls="button";text="转为图像";left=916;top=403;right=1014;bottom=435;z=10};
button3={cls="button";text="遍历文本分块";left=916;top=458;right=1014;bottom=490;z=11};
edit={cls="edit";text="1";left=936;top=310;right=979;bottom=329;edge=1;z=6};
edit2={cls="edit";text="Edit";left=950;top=206;right=951;bottom=207;edge=1;z=5};
richedit2={cls="richedit";text="RichEdit";left=878;top=10;right=1043;bottom=267;edge=1;hscroll=1;multiline=1;vscroll=1;z=1};
static={cls="static";text="Static";left=177;top=14;right=861;bottom=582;transparent=1;z=3};
static2={cls="static";text="目录";left=21;top=9;right=166;bottom=31;transparent=1;z=4};
static3={cls="static";text="第";left=913;top=312;right=931;bottom=329;transparent=1;z=7};
static4={cls="static";text="页";left=986;top=310;right=1008;bottom=329;transparent=1;z=8};
treeview={cls="treeview";left=18;top=37;right=162;bottom=553;asel=false;bgcolor=16777215;edge=1;z=2}
)
/*}}*/

import fsys.PDFium
import web.view

wb = web.view(mainForm.static);

var pdfPath = io.fullpath("FoxitPDF_SDK20_Guide.pdf")
wb.go(pdfPath)
reader = fsys.PDFium(pdfPath)

//获取pdf属性表
var meta = reader.getMeta();
mainForm.richedit2.text = '属性表:\n' + table.tostring(meta) + '\n'

//加载目录
var bm = reader.extractBookmarks()
mainForm.treeview.insertItem( bm.asTree() )

//小工具
mainForm.edit.addCtrl(
    spin={
        cls="spin";marginRight=4;marginTop=1;marginBottom=4;width=16;
        oncommand = function(id,event,pos){
            if( pos && event == 0x4/*_SB_THUMBPOSITION*/ ){
                mainForm.edit.text = tostring(pos)
            }
        }
    }
)

mainForm.edit.spin.setRange(1,reader.pageCount); //设置数值的上下限
mainForm.edit.spin.pos = 1;//设置数值
mainForm.edit.spin.inc  = 1;//设置每次点击箭头的增减量（步长）
mainForm.edit.setPadding(2,18);//设置文本框内边距


//pdf合并工具 todo
mainForm.button.oncommand = function(id,event){
	reader.pageNum = ( tonumber( mainForm.edit.text) -1 )
	var text = reader.extractText();
	var frmText = mainForm.loadForm("\dlg\frmText.aardio");
	frmText.richedit.text = text
	frmText.show();
}

mainForm.button2.oncommand = function(id,event){
    reader.pageNum = ( tonumber( mainForm.edit.text) -1 )
	var bitmap = reader.asBitmap()
	bitmap.save("./out.jpg",100)
	import process
	process.exploreSelect("out.jpg")
}

mainForm.button3.oncommand = function(id,event){
    import console
    reader.pageNum = ( tonumber( mainForm.edit.text) -1 );
    for left,top,right,bottom,text in reader.eachTextRect(){
    	console.log(left,top,right,bottom,text)
    }
}

mainForm.show();
return win.loopMessage();